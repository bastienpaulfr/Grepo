plugins {
    id "groovy"
    id "maven-publish"
    id "jacoco"
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

// Groovy and publishing plugins
apply plugin: "com.jfrog.artifactory"

// group and version used by maven-publish plugin
group = 'fr.coppernic.utils'
version = '0.1.1'

// Code compilation dependencies
dependencies {
    compile localGroovy()
    compile group:'org.eclipse.jgit', name:'org.eclipse.jgit', version:'4.4.1.201607150455-r'
    compile 'commons-io:commons-io:2.4'
    compile 'org.slf4j:slf4j-api:1.7.9'

    testCompile 'junit:junit:4.12'
    testCompile 'org.gperfutils:gprof:0.3.1-groovy-2.4'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

// Code coverage
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled = true
    }
}

tasks.jacocoTestReport {
    dependsOn 'check'
}
tasks.build {
    dependsOn 'jacocoTestReport'
}


// Configuration of a maven publish
publishing {
    publications {
        // name(MavenPublication)
        maven(MavenPublication) {
            from components.java
        }
    }
}

// To publis to a remote location
publishing {
    repositories {
        maven {
            url "../repo"
        }
    }
}

if (project.properties.containsKey("artifactory_user")
        && project.properties.containsKey("artifactory_password")) {
// Configuration of artifactory publish
    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        //The base Artifactory URL if not overridden by the publisher/resolver
        publish {
            repository {
                repoKey = 'libs-release-local'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications('maven')
                publishArtifacts = true
                publishPom = true
            }
        }
    }

// Task dependency
    artifactoryPublish.dependsOn(['build'])
}